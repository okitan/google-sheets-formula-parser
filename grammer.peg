start 
  = additive_expr

additive_expr
  = head:multiplicative_expr tails:(SP? ADDITIVE_OPERATOR SP? multiplicative_expr)* {
      return objects.buildUnaryExpression({ literal: text(), head, tails })
    } 

multiplicative_expr
  = head:primary_expr tails:(SP? MULTIPLICATIVE_OPERATOR SP? primary_expr)* {
      return objects.buildUnaryExpression({ literal: text(), head, tails })
    }

primary_expr
  = elem
  / LPAREN SP? e:additive_expr SP? RPAREN { 
    return { ...e, literal: text() };
  }

// a1 involves number
elem 
  = a1_notation/ number / string

//----
// General
//----
SP
  = [' '\t\r\n]+

LPAREN
  = "("

RPAREN
  = ")"

//----
// Number
//----

number
  = float / integer

// Number core
DIGIT
  = ZERO / NONZERO_DIGIT

ZERO
  = "0"

NONZERO_DIGIT
  = [1-9]

PLUS
  = "+"

MINUS
  = "-"

DECIMAL_POINT
  = "."

// integer
integer
  = s:SIGN i:signed_integer  {
    const negative = s.filter((e) => e === "-").length % 2 === 1;
    return objects.Number({ 
      literal: text(),
      value: i.value * (negative ? -1 : 1),
      negative
    })
  }

SIGN
  = (PLUS / MINUS)*

signed_integer
  = (ZERO  / (NONZERO_DIGIT DIGIT*)) { return new objects.parseAsNumber(text()) }

// float
float
  = i:integer f:frac {
    return objects.Number({
      literal: text(),
      value: i.value + f.value * (i.negative ? -1 : 1)
    });
  } 

frac
  = DECIMAL_POINT DIGIT+ { return new objects.parseAsNumber(`0${text()}`) }

//----
// string
//----

ALPHA 
  = LOWER_ALPHA / UPPER_ALPHA
LOWER_ALPHA 
  = [a-z]
UPPER_ALPHA 
  = [A-Z]

ALNUM = ALPHA / DIGIT

CHAR 
  = ALNUM / [_!@#$%^&*()\-+=\[\]{};:',.<>/?\\' '\t\r\n\u0080-\uFFFF]
ESCAPED_CHAR 
  = '""'

DOUBLE_QUOTE 
  = '"'

// string
string = DOUBLE_QUOTE s:(CHAR / ESCAPED_CHAR)* DOUBLE_QUOTE { return objects.String({ literal: text(), value: s.join("") }) }


//----
// symbol
//----

// range involves cell
a1_notation 
  = range/ cell

DELIMITER
  = ":"

COLUMN
  = [A-Z]+

ROW
  = NONZERO_DIGIT DIGIT*

cell
  = COLUMN ROW { return objects.parseNotation(text()) }

range
  = (COLUMN ROW?) DELIMITER (COLUMN ROW?) { return objects.parseNotation(text()) }
  / (COLUMN? ROW) DELIMITER (COLUMN? ROW) { return objects.parseNotation(text()) }

//----
// Operator
//----

OPERATOR
  = ADDITIVE_OPERATOR

ADDITIVE_OPERATOR
  = PLUS / MINUS / AMBASSADOR

MULTIPLICATIVE_OPERATOR
  = MULTIPLY / DIVIDE

MULTIPLY
  = "*"

DIVIDE
  = "/"

AMBASSADOR
  = "&"